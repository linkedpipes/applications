buildscript {
    ext {
        springBootVersion = '2.0.4.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'com.palantir.docker-run' version '0.20.1'
}

configurations { codacy }

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: "jacoco"

group = 'com.linkedpipes.lpa'
version = '0.0.1'
sourceCompatibility = 10
targetCompatibility = 10

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "http://dl.bintray.com/typesafe/maven-releases" }
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    runtime 'org.springframework.boot:spring-boot-devtools'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    compile 'com.google.code.gson:gson:2.8.5'
    compile 'org.apache.jena:apache-jena-libs:3.8.0'
    compile 'org.apache.jena:jena-querybuilder:3.9.0'
    codacy 'com.github.codacy:codacy-coverage-reporter:-SNAPSHOT'
}

sourceSets {
    main {
        resources {
            srcDir "src/main/config"
        }
    }
}

test {
    include "**/UnitTests.class"
}

// Unpacking solution to avoid 64k files limit in jars with spring boot

task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/dependency")
}

dockerRun {
    name 'my-container'
    image 'linkedpipes/applications:discovery'
    ports '9000:9000'
    daemonize true
    command "-Dldcp.etl.hostname=http://etl:1234", "-Dplay.crypto.secret=yourRandomSecret"
}

// Jacoco setup

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }
}

// Codacy codecov auto upload

task sendCoverageToCodacy(type: JavaExec, dependsOn: jacocoTestReport) {
    main = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy
    args = [
            "report",
            "-l",
            "Java",
            "-r",
            "${buildDir}/reports/jacoco/test/jacocoTestReport.xml",
            "-t",
            "87ac72b5a8d347b5a10a519323d71b6f"
    ]
}